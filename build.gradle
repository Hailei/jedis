apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

group = 'net.heartsavior'
archiveBaseName = 'jedis'
version = '2.2.1.1'

repositories {
   mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile 'org.apache.commons:commons-pool2:2.0'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'jedis'
               packaging 'jar'
               description 'Jedis is a blazingly small and sane Redis java client.'
               url 'http://github.com/xetorthio/jedis'

               scm {
                   url 'scm:git@github.com:HeartSaVioR/jedis.git'
                   connection 'scm:git@github.com:HeartSaVioR/jedis.git'
                   developerConnection 'scm:git@github.com:HeartSaVioR/jedis.git'
               }

               licenses {
                   license {
                       name 'MIT'
                       url 'http://github.com/xetorthio/jedis/raw/master/LICENSE.txt'
                       distribution 'repo'
                   }
               }
           }
        }
    }
}

/*
TODO: Customize for deployment
uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file://localhost/repo/") {
            authentication(userName: "user", password: "pass")
        }
    }
}
*/

task createWrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

